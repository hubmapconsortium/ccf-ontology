PREFIX ccf: <http://purl.org/ccf/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?ct ?ct_label ?as ?as_label
   (GROUP_CONCAT(DISTINCT ?sample_comment; SEPARATOR=", ") AS ?sample_comments)
   (GROUP_CONCAT(DISTINCT ?sample_url; SEPARATOR=", ") AS ?sample_urls)
WHERE {
   ?ct rdfs:subClassOf [
      owl:onProperty obo:RO_0015004 ;  # has_characterizing_marker_set
      owl:someValuesFrom ?characterizing_biomarker_set ] .
   ?characterizing_biomarker_set owl:intersectionOf ?list .
   ?list rdf:rest*/rdf:first/owl:someValuesFrom ?x .
   FILTER NOT EXISTS {
      ?list rdf:rest*/rdf:first/owl:someValuesFrom ?element .
      FILTER ( !(?element IN ({{biomarkers}}) ))
   } .

   ?ct rdfs:label ?ct_label ;
       ccf:ccf_located_in ?as.
   ?as rdfs:label ?as_label .

   OPTIONAL {
      ?spatial_entity ccf:collides_with ?as ;
                      ccf:represents_bbox_of ?sample .
      ?sample rdfs:comment ?sample_comment ;
              ccf:url ?sample_url .
   }
}
group by ?ct ?ct_label ?as ?as_label